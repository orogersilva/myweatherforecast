plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
}

android {

    compileSdk buildConfig.compileSdk

    defaultConfig {

        applicationId "com.orogersilva.myweatherforecast"

        minSdk buildConfig.minSdk
        targetSdk buildConfig.targetSdk

        versionCode appVersion.versionCode
        versionName appVersion.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    lintOptions {

        abortOnError true
        checkDependencies true
        checkReleaseBuilds false
        htmlOutput file("lint-report.html")
        htmlReport false
        lintConfig file("${project.rootDir}/quality/lint.xml")
        warningsAsErrors true
        xmlReport true
    }

    signingConfigs {

        release {

            storeFile rootProject.file('keystore/release.jks')
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {

        debug {

            testCoverageEnabled false

            debuggable true

            buildConfigField "Boolean", "IS_DEBUG_BUILD", "true"
            buildConfigField "Boolean", "IS_RELEASE_BUILD", "false"

            applicationIdSuffix ".dev"
            versionNameSuffix '-DEV'
        }

        release {

            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "Boolean", "IS_DEBUG_BUILD", "false"
            buildConfigField "Boolean", "IS_RELEASE_BUILD", "true"

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion versions.androidComposeCompilerVersion
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation project(':feature:daily')
    implementation project(':feature:weekly')
    implementation project(':navigation')

    debugImplementation deps.android.test.composeManifest

    implementation deps.dagger.hilt.android
    kapt deps.dagger.hilt.compiler
    implementation deps.googlePlayServices.location
    implementation deps.timber.core
}