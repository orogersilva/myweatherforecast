plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {

    compileSdk buildConfig.compileSdk

    defaultConfig {

        applicationId "com.orogersilva.myweatherforecast"

        minSdk buildConfig.minSdk
        targetSdk buildConfig.targetSdk

        versionCode appVersion.versionCode
        versionName appVersion.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    lintOptions {

        abortOnError true
        checkDependencies true
        checkReleaseBuilds false
        htmlOutput file("lint-report.html")
        htmlReport false
        lintConfig file("${project.rootDir}/quality/lint.xml")
        warningsAsErrors true
        xmlReport true
    }

    signingConfigs {

        release {

            storeFile rootProject.file('keystore/release.jks')
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {

        debug {

            testCoverageEnabled false

            debuggable true

            applicationIdSuffix ".dev"
            versionNameSuffix '-DEV'
        }

        release {

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion versions.androidComposeVersion
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation deps.android.activity.activityCompose
    implementation deps.android.compose.material3
    implementation deps.android.compose.ui
    debugImplementation deps.android.compose.uiTooling
    implementation deps.android.compose.uiToolingPreview
    implementation deps.android.ktx.core
    debugImplementation deps.android.test.composeManifest

    testImplementation deps.junit.core

    androidTestImplementation deps.android.test.compose
    androidTestImplementation deps.android.test.espresso
    androidTestImplementation deps.junit.ext
}